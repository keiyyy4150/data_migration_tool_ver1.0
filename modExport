Option Explicit

Public Sub ExportCSV()
    On Error GoTo EH
    If Len(CurrentMasterTableName) = 0 Then UsePipelineMain
    
    Dim masterTbl As ListObject
    Set masterTbl = ThisWorkbook.ListObjects(CurrentMasterTableName)
    If masterTbl.DataBodyRange Is Nothing Then
        MsgBox "出力対象のデータがありません。", vbExclamation
        Exit Sub
    End If
    
    Dim basePath As String, exportPath As String, autoName As String, tStart As Date
    tStart = Now
    basePath = ThisWorkbook.Path
    exportPath = basePath & "\" & EXPORT_FOLDER & "\"
    EnsureFolder exportPath
    EnsureFolder basePath & "\" & LOG_FOLDER & "\"
    
    If IsProcessed Then
        autoName = CurrentExportPrefix & "export_processed_" & TimeStamp() & ".csv"
    Else
        autoName = CurrentExportPrefix & "export_raw_" & TimeStamp() & ".csv"
    End If
    
    WriteRangeAsCsvWithEncoding masterTbl, exportPath & autoName, CurrentEncodingMode
    WriteLog "INFO", "ExportCSV: " & exportPath & autoName & " / Encoding=" & CurrentEncodingMode & " / rows=" & masterTbl.DataBodyRange.Rows.Count & " / elapsed=" & Format(Now - tStart, "hh:nn:ss")
    MsgBox "CSV出力しました：" & exportPath & autoName, vbInformation
    Exit Sub
EH:
    WriteLog "ERROR", "ExportCSV error: " & Err.Number & " " & Err.Description
    MsgBox "ExportCSVでエラー: " & Err.Description, vbCritical
End Sub